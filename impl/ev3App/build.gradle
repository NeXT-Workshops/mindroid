apply plugin: 'java'

apply from: file('../include/common/build.gradle')

version = ext.mindroidVersion
sourceCompatibility = ext.javaSourceLevel
targetCompatibility = ext.javaTargetLevel
compileJava.options.encoding = ext.javaFileEncoding

configurations {
    sshAntTask
}

jar {
    baseName = 'org.mindroid.ev3App'
    version = rootProject.ext.mindroidVersion
    manifest {
        attributes 'Main-Class': 'mindroid.common.ev3.app.App'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

repositories {
    mavenCentral()
}

dependencies {
    compile project(':ev3Messages')
    compile 'com.esotericsoftware.kryo:kryo:2.24.0'
    compile 'com.esotericsoftware:kryonet:2.22.0-RC1'
    compile files('lib/ev3classes-0.9.1-beta.jar')
    compile files('lib/dbusjava-2.7.jar')
    sshAntTask 'org.apache.ant:ant-jsch:1.7.1', 'jsch:jsch:0.1.29'
}

task buildJar {
    jar
}


task deployEV3(dependsOn: jar) << {
    ant.taskdef(name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp', classpath: configurations.sshAntTask.asPath)
    ant.scp(todir: ev3_username+'@'+ev3_server+':/home/lejos/programs',
            password: ev3_password,
            verbose: 'true') {
        fileset(dir: './build/libs') {
            include(name: '**/*.jar')
        }
    }
}

